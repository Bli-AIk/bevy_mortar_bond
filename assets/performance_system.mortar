// ==============================================================================
// Performance System Example
// ==============================================================================
// This example demonstrates the new performance system features in Mortar,
// including event definitions, timelines, run statements, and with events.

// ------------------------------------------------------------------------------
// Event Definitions
// ------------------------------------------------------------------------------
// Events are reusable actions that can be triggered independently or attached
// to text. They can have an index (for text association) and a duration.

event SetColor {
    index: 0
    action: set_color("#228B22")
}
event SetColor2 {
    index: 0
    action: set_color("#FFFFFF")
}

event TPRight {
    action: set_animation("right")
    duration: 2.0
}

event TPLeft {
    action: set_animation("left")
    duration: 1.5
}

event PlaySound {
    action: play_sound("random-sound-1-80941.wav")
    duration: 0.0
}

event SetBlack {
    action: set_color("#000000")
    duration: 1.0
}

// ------------------------------------------------------------------------------
// Timeline Definitions
// ------------------------------------------------------------------------------
// Timelines are sequences of events and wait statements that execute in order.
// They're perfect for complex cutscenes or animations.

timeline OpeningCutscene {
    run TPRight
    wait 1.0
    run TPLeft
    wait 0.5
    run PlaySound
    wait 2.0
    run SetColor2
}

timeline EndingCutscene {
    run SetBlack
    wait 1.5
    run PlaySound
    wait 3.0
}

// ------------------------------------------------------------------------------
// Node Examples
// ------------------------------------------------------------------------------

// Example 1: Basic run statement
// The 'run' keyword executes an event immediately, ignoring its index.
node Start {
    text: "The scene begins..."

    //
    run TPRight
    // Execute immediately, then execute the next one
    run SetColor

    text: "Alice appears in the forest."
} -> WithEventsExample

// Example 2: With events (text association)
// The 'with' keyword associates events with the previous text.
// Events are sorted by their index and executed as the text displays.
node WithEventsExample {
    text: "Welcome to the adventure!"
    with SetColor2  // Single event

    text: "The forest comes alive with sound and color."
    with events: [
        TPRight
        PlaySound
    ]
} -> CustomIndexExample

// Example 3: Run with custom index
// You can override an event's index when running it.
node CustomIndexExample {
    text: "This text has a custom event timing."
    let custom_time: Number = 0.5
    run TPRight with custom_time
} -> ReferenceExample

// Example 4: Run with reference
// Using 'ref' creates a dynamic reference that can change at runtime.
node ReferenceExample {
    text: "Dynamic event timing."
    let dynamic_index: Number = 1.0
    run TPLeft with ref dynamic_index
} -> MixedExample

// Example 5: Mixed usage
node MixedExample {
    // Direct run (executes immediately)
    run SetColor

    // Text with associated event
    text: "A figure emerges from the shadows..."
    with TPRight

    // Another text block
    text: "It's Alice!"

    // Run with custom index
    let reveal_time: Number = 0.3
    run PlaySound with reveal_time

    // Choice with event
    choice: [
        "Approach Alice" -> AliceRoute
        "Hide" -> HideRoute
        "Waiting..." -> break
    ]

    text: "well, let's go."
} -> TimelineExample

// Example 6: Timeline in node
node TimelineExample {
    text: "Watch the opening cutscene..."
    run OpeningCutscene  // Execute the entire timeline
}

// Placeholder nodes for choices
node AliceRoute {
    text: "You approach Alice..."
}

node HideRoute {
    text: "You hide behind a tree..."
}

// ------------------------------------------------------------------------------
// Function Declarations
// ------------------------------------------------------------------------------
// All functions used in events must be declared.
// These correspond to the actions in handle_mortar_action in the bevy binding.

fn set_animation(anim_name: String)
fn set_color(color: String)
fn play_sound(file_name: String)
